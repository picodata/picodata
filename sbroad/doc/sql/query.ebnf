statement   ::= explain | ddl | dml | dql | acl | call
explain     ::= 'EXPLAIN' (dml | dql)
dql         ::= (with? select | values)
                ('OPTION' '('
                    (('sql_motion_row_max' | 'sql_vdbe_opcode_max') '=' unsigned)
                    (',' (('sql_motion_row_max' | 'sql_vdbe_opcode_max') '=' unsigned))*
                ')')?
with        ::= 'WITH'
                cte ('(' column (',' column)* ')')? 'AS' '(' (select | values) ')'
                (',' cte ('(' column (',' column)* ')')? 'AS' '(' (select | values) ')')*
select      ::= 'SELECT' 'DISTINCT'? projection (',' projection)* from?
                ('LIMIT' (unsigned | 'ALL' | 'NULL'))?
from        ::= 'FROM' scan
                (( 'INNER' | 'LEFT' 'OUTER'? )? 'JOIN' scan 'ON' expression)*
                ('WHERE' expression)?
                ('GROUP' 'BY' expression (',' expression)*)?
                ('HAVING' expression)?
                ('WINDOW' name 'AS' '(' partition ')')?
                ('ORDER' 'BY' expression ('ASC' | 'DESC')? (',' expression ('ASC' | 'DESC')? ('NULLS' ('FIRST' | 'LAST'))?)*)?
                (('UNION' 'ALL'? | 'EXCEPT' 'DISTINCT'?) select)?
                ('LIMIT' (unsigned | 'ALL' | 'NULL'))?
projection  ::= (table '.')? '*' | expression (('AS')? name)? | aggregate | window (('AS')? name)?
scan        ::= (table | '(' dql ')') ('AS'? name)?
expression  ::= ('NOT'* (
                    (table '.')? column
                    | case
                    | cast
                    | coalesce
                    | current_date
                    | like
                    | literal
                    | localtimestamp
                    | lower
                    | ilike
                    | instance_uuid
                    | pico_instance_uuid
                    | pico_raft_leader_id
                    | pico_raft_leader_uuid
                    | substr
                    | substring
                    | to_char
                    | to_date
                    | trim                    
                    | upper
                    | version
                    | 'NOT'? 'EXISTS' '(' dql ')'
                    | '(' dql ')'
                    | '(' expression (',' expression)* ')'
                ) ('IS' 'NOT'? ('NULL' | 'TRUE' | 'FALSE' | 'UNKNOWN'))*)
                | expression
                (
                    'NOT'? 'BETWEEN' expression 'AND'
                    | ('+' | '-' | '*' | '/' | '%')
                    | ('<>' | '!=' | '>=' | '>' | '<=' | '<' | '=' | 'NOT'? 'IN')
                    | '||'
                    | 'AND'
                    | 'OR'
                )
                expression
aggregate   ::= ('AVG' | 'COUNT' | 'MAX' | 'MIN' | 'SUM' | 'TOTAL') '(' ('DISTINCT'? expression) ')'
               | ('GROUP_CONCAT' | 'STRING_AGG') '(' expression ',' "'" string "'" ')'
window      ::= (builtin | aggregate) ('FILTER' '(' 'WHERE' expression ')')?
                 'OVER' (name | ('(' partition ')'))
partition   ::= ('PARTITION' 'BY' expression (',' expression)*)?
                 ('ORDER' 'BY' expression ('ASC' | 'DESC')? (',' expression ('ASC' | 'DESC')?)*)?
                 (('RANGE' | 'ROWS') (
                   (
                      'BETWEEN'
                      (
                        ('UNBOUNDED' 'PRECEDING')
                        | (expression ('FOLLOWING' | 'PRECEDING'))
                        | ('CURRENT' 'ROW')
                      )
                      'AND'
                      (
                        ('UNBOUNDED' 'FOLLOWING')
                        | (expression ('FOLLOWING' | 'PRECEDING'))
                        | ('CURRENT' 'ROW')
                      )
                   ) | ('UNBOUNDED' 'PRECEDING') | ('CURRENT' 'ROW') | ( expression 'PRECEDING')
                 ))?
builtin     ::= 'LAST_VALUE' '(' expression ')'
                | 'ROW_NUMBER' '(' ')'
case        ::= 'CASE' expression?
                ('WHEN' expression 'THEN' expression)+
                ('ELSE' expression)? 'END'
cast        ::= 'CAST' '(' expression 'AS' type ')' | expression '::' type
coalesce    ::= 'COALESCE' '(' expression ',' expression (',' expression)* ')'
like        ::= expression 'LIKE' expression ('ESCAPE' expression)?
localtimestamp ::= 'LOCALTIMESTAMP' ('(' unsigned ')')?
ilike       ::= expression 'ILIKE' expression ('ESCAPE' expression)?
instance_uuid    ::= 'INSTANCE_UUID' '(' ')'
pico_instance_uuid    ::= 'PICO_INSTANCE_UUID' '(' ')'
to_char     ::= 'TO_CHAR' '(' expression ',' format ')'
to_date     ::= 'TO_DATE' '(' expression ',' format ')'
trim        ::= 'TRIM' '('
                ((('LEADING' | 'TRAILING' | 'BOTH')? removal_chars
                | ('LEADING' | 'TRAILING' | 'BOTH')) 'FROM')? string ')'
substr      ::= 'SUBSTR' '(' string ',' from (',' count)? ')'
substring   ::= 'SUBSTRING' '(' substringVariant ')'
substringVariant ::= substringFromFor | substringRegular | substringFor | substringFrom | substringSimilar
substringFromFor ::= expression 'FROM' expression 'FOR' expression
substringRegular ::= expression ',' expression ',' expression
substringFor ::= expression 'FOR' expression
substringFrom ::= expression ('FROM' | ',') expression
substringSimilar ::= expression 'SIMILAR' expression 'ESCAPE' expression
lower       ::= 'LOWER' '(' string ')'
upper       ::= 'UPPER' '(' string ')'
values      ::= 'VALUES'
                ('(' (expression(',' expression)*) ')')
                (',' ('(' (expression(',' expression)*) ')'))*
literal     ::= 'TRUE'
                | 'FALSE'
                | 'NULL'
                | decimal
                | double
                | integer
                | string
                | unsigned
                | ('$' unsigned)
                | '?'
dml         ::= (call | delete | insert | update)
                ('OPTION' '('
                    (('sql_motion_row_max' | 'sql_vdbe_opcode_max') '=' unsigned)
                    (',' (('sql_motion_row_max' | 'sql_vdbe_opcode_max') '=' unsigned))*
                ')')?
call        ::= 'CALL' procedure '(' (literal (',' literal)*)? ')'
delete      ::= 'DELETE' 'FROM' table ('WHERE' expression)?
insert      ::= 'INSERT' 'INTO' table ('(' column (',' column)* ')')? dql
                ('ON' 'CONFLICT' 'DO' ('NOTHING' | 'REPLACE' | 'FAIL'))?
update      ::= 'UPDATE' table 'SET'
                column '=' expression (',' column '=' expression)*
                ('FROM' scan)? ('WHERE' expression)?
acl         ::= (alter_user | create_role | create_user | drop_role | drop_user | grant | revoke)
                ('OPTION' '(' ('TIMEOUT' '=' double)')')?
revoke      ::= 'REVOKE' (
                    (('CREATE' | 'ALTER' | 'DROP') 'USER')
                    | (('ALTER' | 'DROP') 'ON' 'USER' user)
                    | (('CREATE' | 'DROP') 'ROLE')
                    | ('DROP' 'ON' 'ROLE' role)
                    | (('CREATE' | 'ALTER' | 'DROP' | 'READ' | 'WRITE') 'TABLE')
                    | (('ALTER' | 'DROP' | 'READ' | 'WRITE') 'ON' 'TABLE' table)
                    | (('CREATE' | 'EXECUTE' | 'DROP') 'PROCEDURE')
                    | (('EXECUTE' | 'DROP') 'ON' 'PROCEDURE' procedure ('(' type (',' type)* ')')?)
                    | role
                )
                'FROM' (role | user)
grant       ::= 'GRANT' (
                    (('CREATE' | 'ALTER' | 'DROP') 'USER')
                    | (('ALTER' | 'DROP') 'ON' 'USER' user)
                    | (('CREATE' | 'DROP') 'ROLE')
                    | ('DROP' 'ON' 'ROLE' role)
                    | (('CREATE' | 'ALTER' | 'DROP' | 'READ' | 'WRITE') 'TABLE')
                    | (('ALTER' | 'DROP' | 'READ' | 'WRITE') 'ON' 'TABLE' table)
                    | (('CREATE' | 'EXECUTE' | 'DROP') 'PROCEDURE')
                    | (('EXECUTE' | 'DROP') 'ON' 'PROCEDURE' procedure ('(' type (',' type)* ')')?)
                    | role
                )
                'TO' (role | user)
ddl         ::= (alter_plugin | alter_procedure | alter_system | alter_table
                | create_index | create_plugin | create_procedure | create_table
                | drop_index | drop_plugin | drop_procedure | drop_table | truncate_table)
alter_system ::= 'ALTER' 'SYSTEM'
                    (
                        'RESET' ('ALL' | param_name)
                        | 'SET' param_name ('=' | 'TO') ('DEFAULT' | param_value)
                    )
                    ('FOR' ('ALL' 'TIERS' | 'TIER' tier))?
alter_procedure ::= 'ALTER' 'PROCEDURE' procedure ('(' type (',' type)* ')')?
                     'RENAME' 'TO' procedure
                     ('WAIT' 'APPLIED' ('GLOBALLY' | 'LOCALLY'))?
                     ('OPTION' '(' ('TIMEOUT' '=' double)')')?
create_index ::= 'CREATE' 'UNIQUE'? 'INDEX' ('IF' 'NOT' 'EXISTS')? index 'ON' table
                 ('USING' ('TREE' | 'HASH' | 'RTREE' | 'BITSET'))?
                 '(' column (',' column)* ')' ('WITH' '('
                     (
                         ('BLOOM_FPR' '=' decimal)
                         | ('PAGE_SIZE' '=' unsigned)
                         | ('RANGE_SIZE' '=' unsigned)
                         | ('RUN_COUNT_PER_LEVEL' '=' unsigned)
                         | ('RUN_SIZE_RATIO' '=' decimal)
                         | ('DIMENSION' '=' unsigned)
                         | ('DISTANCE' '=' ('EUCLID' | 'MANHATTAN'))
                         | ('HINT' '=' ('TRUE' | 'FALSE'))
                     )
                     (
                        ','
                         (
                             ('BLOOM_FPR' '=' decimal)
                             | ('PAGE_SIZE' '=' unsigned)
                             | ('RANGE_SIZE' '=' unsigned)
                             | ('RUN_COUNT_PER_LEVEL' '=' unsigned)
                             | ('RUN_SIZE_RATIO' '=' decimal)
                             | ('DIMENSION' '=' unsigned)
                             | ('DISTANCE' '=' ('EUCLID' | 'MANHATTAN'))
                             | ('HINT' '=' ('TRUE' | 'FALSE'))
                         )
                     )*
                 ')')?
                 ('WAIT' 'APPLIED' ('GLOBALLY' | 'LOCALLY'))?
                 ('OPTION' '(' ('TIMEOUT' '=' double)')')?
create_procedure ::= 'CREATE' 'PROCEDURE' ('IF' 'NOT' 'EXISTS')? procedure '(' type (',' type)* ')'
                     ('LANGUAGE' 'SQL')? (
                         ('AS' '$$' (insert | update | delete) '$$')
                         | ('BEGIN' 'ATOMIC' (insert | update | delete) 'END')
                     )
                     ('WAIT' 'APPLIED' ('GLOBALLY' | 'LOCALLY'))?
                     ('OPTION' '(' ('TIMEOUT' '=' double)')')?
create_role    ::= 'CREATE' 'ROLE' ('IF' 'NOT' 'EXISTS')? role
create_table   ::= 'CREATE' 'TABLE' ('IF' 'NOT' 'EXISTS')? table
                   '('
                       column type ('NOT'? 'NULL')? ('PRIMARY' 'KEY')? (',' column type ('NOT'? 'NULL')? ('PRIMARY' 'KEY')?)*
                       (',' 'PRIMARY' 'KEY' '(' column (',' column)* ')')?
                   ')'
                   ('USING' ('MEMTX' | 'VINYL'))?
                   (('DISTRIBUTED' (('BY' '(' column (',' column)* ')' ('IN' 'TIER' tier)?) | 'GLOBALLY'))?)?
                   ('WAIT' 'APPLIED' ('GLOBALLY' | 'LOCALLY'))?
                   ('OPTION' '(' ('TIMEOUT' '=' double)')')?
create_user    ::= 'CREATE' 'USER' ('IF' 'NOT' 'EXISTS')? user (('USING' ('LDAP')) | ('WITH'? 'PASSWORD' "'" password "'" ('USING' ('CHAP-SHA1' | 'MD5')?)?))
alter_user     ::= 'ALTER' 'USER' user
                   'WITH'? (
                       'LOGIN'
                       | 'NOLOGIN'
                       | 'PASSWORD' (('USING' ('LDAP')) | ("'" password "'" ('USING' ('CHAP-SHA1' | 'MD5'))?))
                       | 'RENAME' 'TO' user
                   )
drop_index     ::= 'DROP' 'INDEX' ('IF' 'EXISTS')? index
                   ('WAIT' 'APPLIED' ('GLOBALLY' | 'LOCALLY'))?
                   ('OPTION' '(' ('TIMEOUT' '=' double)')')?
drop_procedure ::= 'DROP' 'PROCEDURE' ('IF' 'EXISTS')? procedure ('(' type (',' type)* ')')?
                   ('WAIT' 'APPLIED' ('GLOBALLY' | 'LOCALLY'))?
                   ('OPTION' '(' ('TIMEOUT' '=' double)')')?
drop_table     ::= 'DROP' 'TABLE' ('IF' 'EXISTS')? table
                   ('WAIT' 'APPLIED' ('GLOBALLY' | 'LOCALLY'))?
                   ('OPTION' '(' ('TIMEOUT' '=' double)')')?
truncate_table ::= 'TRUNCATE' ('TABLE')? table
                   ('WAIT' 'APPLIED' ('GLOBALLY' | 'LOCALLY'))?
                   ('OPTION' '(' ('TIMEOUT' '=' double)')')?
alter_table    ::= 'ALTER' 'TABLE' table
                   (
                       ('RENAME' (('COLUMN' column 'TO' column)? | ('TO' table)?))
                       | (
                            ('ADD' 'COLUMN'? ('IF' 'NOT' 'EXISTS')? column type ('NOT' 'NULL')?)
                            (
                            ','
                                (
                                    ('ADD' 'COLUMN'? ('IF' 'NOT' 'EXISTS')? column type ('NOT' 'NULL')?)
                                )
                            )*
                       )
                   )
                   ('WAIT' 'APPLIED' ('GLOBALLY' | 'LOCALLY'))?
                   ('OPTION' '(' ('TIMEOUT' '=' double)')')?
drop_role      ::= 'DROP' 'ROLE' ('IF' 'EXISTS')? role
drop_user      ::= 'DROP' 'USER' ('IF' 'EXISTS')? user
create_plugin  ::= 'CREATE' 'PLUGIN' ('IF' 'NOT' 'EXISTS')? plugin version
                    ('OPTION' '(' ('TIMEOUT' '=' double)')')?
drop_plugin    ::= 'DROP' 'PLUGIN' ('IF' 'EXISTS')? plugin version ('WITH' 'DATA')?
                    ('OPTION' '(' ('TIMEOUT' '=' double)')')?
alter_plugin  ::= 'ALTER' 'PLUGIN' plugin (
                        (version (
                            'ENABLE' | 'DISABLE'
                            | ('ADD' 'SERVICE' service 'TO' 'TIER' tier)
                            | ('REMOVE' 'SERVICE' service 'FROM' 'TIER' tier)
                            | ('SET' ((service'.'key '=' text) (',' service'.'key '=' text)*))
                        ))
                        ('OPTION' '(' ('TIMEOUT' '=' double)')')?
                        | ('MIGRATE' 'TO' version)
                          ( 'OPTION' '('
                            (('TIMEOUT' | 'ROLLBACK_TIMEOUT') '=' double)
                            (',' (('TIMEOUT' | 'ROLLBACK_TIMEOUT') '=' double))*')'
                          )?
                   )
type        ::= 'BOOL'
                | 'BOOLEAN'
                | 'DATETIME'
                | 'DECIMAL'
                | 'DOUBLE'
                | 'INT'
                | 'INTEGER'
                | 'JSON'
                | 'STRING'
                | 'TEXT'
                | 'UNSIGNED'
                | 'UUID'
                | 'VARCHAR' '(' length ')'
